@Metadata.layer: #CORE
@UI: {
  headerInfo: {
    typeName: 'Travel',
    typeNamePlural: 'Travels',
    title: {
        type: #STANDARD, value: 'Description'
    },
    description: {
        value: 'TravelID'
    }
  },
  presentationVariant: [{
    sortOrder: [{
      by: 'LocalLastChangedAt',
      direction: #DESC
    }],
    visualizations: [{
      type: #AS_LINEITEM
    }]
  }]
}
annotate view ZC_FE_TRAVEL_000099 with
{
  @UI.facet: [
    {
      id: 'TravelHeaderPrice',
      purpose: #HEADER,
      type: #DATAPOINT_REFERENCE,
      position: 10,
      targetQualifier: 'PriceData'
    },
    {
      id: 'TravelHeaderOverallStatus',
      purpose: #HEADER,
      type: #DATAPOINT_REFERENCE,
      position: 20,
      targetQualifier: 'StatusData'
    },
    {
      label: 'General Information',
      id: 'GeneralInfo',
      type: #COLLECTION,
      position: 10
    },
    {
      label: 'General',
      id: 'Travel',
      type: #IDENTIFICATION_REFERENCE,
      purpose: #STANDARD,
      parentId: 'GeneralInfo',
      position: 10
    },
    {
      id: 'Dates',
      purpose: #STANDARD,
      type: #FIELDGROUP_REFERENCE,
      parentId: 'GeneralInfo',
      label: 'Dates',
      position: 30,
      targetQualifier: 'DatesGroup'
    },
    {
      id: 'Prices',
      purpose: #STANDARD,
      type: #FIELDGROUP_REFERENCE,
      parentId: 'GeneralInfo',
      label: 'Prices',
      position: 20,
      targetQualifier: 'PricesGroup'
    }    
  ]

  @UI.lineItem: [{ position: 10 }]
  TravelID;

  @UI.identification: [{ position: 10 }]
  Description;

  @UI.identification: [{ position: 30 }]
  @UI.lineItem: [{ position: 20 }]
  @UI.selectionField: [{ position: 10 }]
  AgencyID;

  @UI.identification: [{ position: 20 }]
  @UI.lineItem: [{ position: 30 }]
  @UI.selectionField: [{ position: 20 }]
  CustomerID;

  @UI.fieldGroup: [{ qualifier: 'DatesGroup', position: 10 }]
  @UI.lineItem: [{ position: 40 }]
  BeginDate;

  @UI.fieldGroup: [{ qualifier: 'DatesGroup', position: 20 }]
  @UI.lineItem: [{ position: 50 }]
  EndDate;

  @UI.fieldGroup: [ { qualifier: 'PricesGroup', position: 10} ]
  @UI.lineItem: [{ position: 60 }]
  BookingFee;
  
  @UI.dataPoint: { qualifier: 'PriceData', title: 'Total Price'}
  @UI.fieldGroup: [{ qualifier: 'PricesGroup', position: 20 }]
  @UI.lineItem: [{ position: 70 }]
  TotalPrice;

  @UI.dataPoint: { qualifier: 'StatusData', title: 'Status', criticality: 'OverallStatusCriticality' }
  @UI.lineItem: [{ position: 80, criticality: 'OverallStatusCriticality' }]
  @UI.selectionField: [{ position: 30 }]
  @UI.textArrangement: #TEXT_ONLY
  OverallStatus;

  @UI.lineItem: [{ position: 90 }]
  LocalLastChangedAt;
}